apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'application'
apply plugin: 'idea'

group 'org.sesasar.doma'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.11'
    ext.doma_version = '2.20.0'
    ext.domaResourcesDir = "${buildDir}/tmp/doma-resources"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

mainClassName = 'AppKt'
sourceCompatibility = 1.8

kapt {
    arguments {
        arg("doma.resources.dir", domaResourcesDir)
    }
}

task extractDomaResources(type: Copy, dependsOn: processResources)  {
    from processResources.destinationDir
    include 'doma.compile.config'
    include 'META-INF/**/*.sql'
    include 'META-INF/**/*.script'
    into domaResourcesDir
}

task copyDomaResources(type: Copy, dependsOn: extractDomaResources)  {
    from domaResourcesDir
    into compileJava.destinationDir
}

compileKotlin {
    dependsOn copyDomaResources
    inputs.dir domaResourcesDir
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

dependencies {
    kapt "org.seasar.doma:doma:$doma_version"
    implementation "org.seasar.doma:doma:$doma_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    runtimeOnly 'com.h2database:h2:1.3.175'
    testImplementation group: 'junit', name: 'junit', version: '4.11'
}

idea {
    module {
        inheritOutputDirs = true
    }
}